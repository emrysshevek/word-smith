class_name StateMachine
extends Node

@export var initial_state: State = null

@onready var state: State = (func get_initial_state() -> State:
	return initial_state if initial_state != null else get_child(0)
).call()


func _ready() -> void:
	for state_node: State in find_children("*", "State"):
		state_node.finished.connect(_transition_to_next_state)
		state_node.set_actor(owner)

	await owner.ready
	state.active = true
	state.enter("")


func _unhandled_input(event: InputEvent) -> void:
	state.handle_input(event)


func _process(delta: float) -> void:
	state.update(delta)


func _physics_process(delta: float) -> void:
	state.physics_update(delta)


func _transition_to_next_state(target_state_path: String, data: Dictionary = {}) -> void:
	if not has_node(target_state_path):
		printerr(owner.name + ": Trying to transition to state " + target_state_path + " but it does not exist.")
		return

	print(owner.name + " transitioning to ", target_state_path, " state from ", state, " state")

	var previous_state_path := state.name
	state.active = false
	state.exit()
	
	state = get_node(target_state_path)
	state.active = true
	state.enter(previous_state_path, data)
